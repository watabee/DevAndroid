import dependencies.Deps
import dependencies.Versions

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "de.mannodermaus.android-junit5"
apply plugin: "scabbard.gradle"
apply plugin: 'dagger.hilt.android.plugin'

ext {
    ci = System.getenv("CI") == "true"
}

scabbard {
    enabled !ci
}

android {
    compileSdkVersion Versions.androidCompileSdkVersion
    buildToolsVersion Versions.buildToolsVersion
    ndkVersion Versions.ndkVersion

    defaultConfig {
        applicationId "com.github.watabee.devapp"
        minSdkVersion Versions.androidMinSdkVersion
        targetSdkVersion Versions.androidTargetSdkVersion
        versionCode Versions.androidVersionCode
        versionName Versions.androidVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile rootProject.file("config/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
        }
        release {
            signingConfig signingConfigs.debug // TODO

            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        xmlReport true
        htmlReport false
        abortOnError false
        checkDependencies true
        checkTestSources true
        checkReleaseBuilds false

        disable "UnusedResources"
    }

    dexOptions {
        // Don't pre-dex on CI
        preDexLibraries !ci
    }

    testOptions {
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }
        }
        unitTests.all {
            testLogging.events = ["passed", "skipped", "failed"]
        }
        unitTests {
            includeAndroidResources = true
        }
    }

    buildFeatures {
        buildConfig = true
        dataBinding = true
    }

    packagingOptions {
        exclude("META-INF/*.kotlin_module")
    }
}

dependencies {
    implementation project(':ui:top')

    implementation Deps.Kotlin.stdlib
    implementation Deps.AndroidX.startup

    implementation Deps.Hilt.android
    kapt Deps.Hilt.compiler

    implementation Deps.timber

    debugImplementation Deps.Flipper.flipper
    debugImplementation Deps.Flipper.networkPlugin
    debugImplementation Deps.soLoader

    testImplementation Deps.junit

    testImplementation Deps.Spek2.dslJvm
    testImplementation Deps.Spek2.runner
    testImplementation Deps.Kotlin.reflect

    androidTestImplementation Deps.AndroidX.Test.runner
    androidTestImplementation Deps.AndroidX.Test.espressoCore
}

hilt {
    enableExperimentalClasspathAggregation = true
}
