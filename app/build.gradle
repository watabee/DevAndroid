import dependencies.Deps
import dependencies.Versions

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "de.mannodermaus.android-junit5"

ext {
    ci = System.getenv("CI") == "true"
}

android {
    compileSdkVersion Versions.androidCompileSdkVersion
    buildToolsVersion Versions.buildToolsVersion
    ndkVersion "21.0.5935234"

    defaultConfig {
        applicationId "com.github.watabee.rakutenapp"
        minSdkVersion Versions.androidMinSdkVersion
        targetSdkVersion Versions.androidTargetSdkVersion
        versionCode Versions.androidVersionCode
        versionName Versions.androidVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {
            storeFile rootProject.file("config/debug.keystore")
            storePassword "android"
            keyAlias "androiddebugkey"
            keyPassword "android"
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.debug
            applicationIdSuffix ".debug"
        }
        release {
            signingConfig signingConfigs.debug // TODO

            shrinkResources true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dexOptions {
        // Don't pre-dex on CI
        preDexLibraries !ci
    }

    testOptions {
        junitPlatform {
            filters {
                engines {
                    include 'spek2'
                }
            }
        }
        unitTests.all {
            testLogging.events = ["passed", "skipped", "failed"]
        }
        unitTests {
            includeAndroidResources = true
        }
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    implementation project(':base')
    implementation project(':base-android')
    implementation project(':data:api')
    implementation project(':ui:common')
    implementation project(':ui:image')
    implementation project(':ui:top')
    implementation project(':ui:ichiba:ranking')

    implementation Deps.Kotlin.stdlib
    implementation Deps.AndroidX.appcompat
    implementation Deps.AndroidX.coreKtx
    implementation Deps.AndroidX.constraintlayout
    implementation Deps.AndroidX.Activity.activityKtx
    implementation Deps.AndroidX.Fragment.fragmentKtx

    implementation Deps.Retrofit.retrofit
    implementation Deps.Moshi.kotlin

    implementation Deps.Dagger.dagger
    kapt Deps.Dagger.compiler
    implementation Deps.Dagger.androidSupport
    kapt Deps.Dagger.androidProcessor
    compileOnly Deps.AssistedInject.annotationDagger2
    kapt Deps.AssistedInject.processorDagger2

    debugImplementation Deps.Flipper.flipper
    debugImplementation Deps.Flipper.networkPlugin
    debugImplementation Deps.soLoader

    testImplementation Deps.junit

    testImplementation Deps.Spek2.dslJvm
    testImplementation Deps.Spek2.runner
    testImplementation Deps.Kotlin.reflect

    androidTestImplementation Deps.AndroidX.Test.runner
    androidTestImplementation Deps.AndroidX.Test.espressoCore
}
