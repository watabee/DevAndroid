// Top-level build file where you can add configuration options common to all sub-projects/modules.
import dependencies.Deps

buildscript {
    repositories {
        google()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath Deps.androidGradlePlugin
        classpath Deps.Kotlin.gradlePlugin
        classpath Deps.androidJunit5GradlePlugin
        classpath Deps.ktlintGradlePlugin
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions {
            jvmTarget = "1.8"
            freeCompilerArgs += '-Xuse-experimental=kotlin.Experimental'
        }
    }
}

subprojects {
    apply plugin: "org.jlleitschuh.gradle.ktlint"

    ktlint {
        version = Deps.Ktlint.version
        android = true
        ignoreFailures = true
        reporters {
            reporter "checkstyle"
        }
        filter {
            exclude("**/generated/**")
        }
    }

    afterEvaluate {
        tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
            it.dependsOn ktlintFormat
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
