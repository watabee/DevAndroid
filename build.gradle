// Top-level build file where you can add configuration options common to all sub-projects/modules.
import dependencies.Deps

buildscript {
    repositories {
        google()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath Deps.androidGradlePlugin
        classpath Deps.Kotlin.gradlePlugin
        classpath Deps.androidJunit5GradlePlugin
        classpath Deps.spotlessGradlePlugin
    }
}

allprojects {
    repositories {
        google()
        maven {
            url "https://oss.sonatype.org/content/repositories/snapshots/"
            content { includeGroup 'com.squareup.moshi' }
        }
        jcenter()
    }

    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile) {
        kotlinOptions {
            jvmTarget = "1.8"
        }
    }
}

subprojects {
    apply plugin: "com.diffplug.gradle.spotless"

    spotless {
        kotlin {
            target "**/*.kt"

            // https://github.com/diffplug/spotless/issues/419
            ktlint("0.33.0")
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task installGitHook(type: Copy) {
    from new File(new File(rootProject.rootDir, 'config'), 'pre-commit')
    into { new File(rootProject.rootDir, '.git/hooks') }
    fileMode 0777
}
tasks.getByPath(':app:preBuild').dependsOn installGitHook
